# Functions/models for computing nullclines of equations defined in Martinez2012
# to model germinal cell center regulation

using PlasmaCellDiff
using UnPack

"""
    blimp1_nullcline(u, params::GerminalCenterODEParams)  

```math
\\begin{equation}
\\dot{p} = 0 \\Leftrightarrow p = \\frac{\\mu_p + \\sigma_p \\frac{k_b^2}{k_b^2 + b^2}}{\\lambda_p}
\\end{equation}
```

Return nullcline for BLIMP1 (`p`).

# References
[1] : Equation S6 from Martinez2012

[2] : https://www.normalesup.org/~doulcier/teaching/modeling/bistable_systems.html
"""
function blimp1_nullcline(u, params::GerminalCenterODEParams)
    # parameters 
    @unpack μp, σp, λp, kb = params

    # transcription factor state variables 
    p, b = u

    kb_scaled = dissociation_scaler(kb, b)
    p_nullcline = (μp + σp*kb_scaled)/λp
 
    return p_nullcline
end 

"""
    bcl6_nullcline(u, params::GerminalCenterODEParams) 


```math
\\begin{equation}
\\dot{b} = 0 \\Leftrightarrow \\frac{k_p \\sqrt{b bcr_0 k_b^2 + (b^2 + k_b^2)(b \\lambda_b - \\mu_b) - k_b^2 \\sigma_b}}{\\sqrt{-b bcr_0 k_b^2 - (b^2 + k_b^2)(b \\lambda_b - \\mu_b)}}
\\end{equation}
```

Return nullcline for BCL6 (`b`). Solution generated by mathematica.

NOTE: Since the solution works out to be `p^2 = RHS`, then the solution should 
be `p = +-sqrt(RHS)`, but since negative protein levels are not physically 
interpretable, only +b_nullcline is returned.

# References
[1] : Equation S7 from Martinez2012

[2] : https://www.normalesup.org/~doulcier/teaching/modeling/bistable_systems.html
"""
function bcl6_nullcline(u, params::GerminalCenterODEParams, t)
    # parameters 
    @unpack kp, kb, σb, μb, λb, bcr0 = params

    # transcription factor state variables 
    p, b = u
    
    b_nullcline_numerator = kp*sqrt(b*bcr0*kb^2 + (b^2 + kb^2)*(b*λb - μb) 
        - kb^2*σb)
    b_nullcline_denominator = sqrt(-b*bcr0*kb^2 - (b^2 + kb^2)*(b*λb - μb))
    b_nullcline = b_nullcline_numerator/b_nullcline_denominator    

    return b_nullcline
end 
